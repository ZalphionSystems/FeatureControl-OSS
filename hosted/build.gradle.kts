plugins {
    id("com.gradleup.shadow")
    id("app.cash.sqldelight")
    id("com.google.devtools.ksp")
    application
}

dependencies {
    api(project(":emails"))
    api("org.flywaydb:flyway-core:_")
    api("org.http4k:http4k-config")
    api("com.zaxxer:HikariCP:_")
    api("org.http4k:http4k-server-undertow")
    api(CashApp.sqlDelight.drivers.jdbc)

    implementation("com.sun.mail:jakarta.mail:_")

    runtimeOnly("org.flywaydb:flyway-database-postgresql:_")
    runtimeOnly("org.postgresql:postgresql:_")
    runtimeOnly("org.webjars.npm:alpinejs:_")
    runtimeOnly("org.webjars.npm:dayjs:_")

    ksp("se.ansman.kotshi:compiler:_")

    testFixturesApi("org.testcontainers:testcontainers-postgresql")

    testImplementation(testFixtures(project(":core")))
}

sqldelight {
    databases {
        create("CoreDatabase") {
            packageName.set("com.zalphion.featurecontrol.storage")
            srcDirs("src/main/sqldelight")
            migrationOutputDirectory = layout.buildDirectory.dir("resources/main/com/zalphion/featurecontrol/core")
            deriveSchemaFromMigrations.set(true)
            dialect(CashApp.sqlDelight.dialects.postgreSql)
        }
    }
}

application {
    mainClass.set("com.zalphion.featurecontrol.CoreMainKt")
}

// flyway depends on migrations generated by sqlDelight
tasks.compileKotlin.configure {
    dependsOn("generateMainCoreDatabaseMigrations")
}

// sqlDelight depends on ksp
tasks.matching { it.name.startsWith("ksp") }.configureEach {
    dependsOn("generateMainCoreDatabaseInterface")
}

tasks.build {
    dependsOn("shadowJar")
}

tasks.shadowJar {
    minimize {
        exclude(dependency("org.postgresql:postgresql:.*"))
        exclude(dependency("org.flywaydb:flyway-database-postgresql:.*"))
        exclude(dependency("org.tinylog:tinylog-impl:.*"))
        exclude(dependency("org.tinylog:slf4j-tinylog:.*"))
        exclude(dependency("com.sun.mail:jakarta.mail:.*"))
        exclude(dependency("io.undertow:undertow-core:.*"))
    }
}